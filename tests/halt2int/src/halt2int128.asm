
; Copyright 2021 Mark Woodmass

; This program is licensed under the GNU General Public License. See the
; file `COPYING' for details

; Only compatible on ZX Spectrum 128K

                        org   33024

start                   proc
                        call  3435
                        ld    a,2
                        call  #1601

                        call  test_float_timing

                        call  print_text_pc
                        db    "ADDR  CYCLE R",6,"ADDR  CYCLE R",13,255

                        ld    hl,results_obtained
                        ld    (results_obtained_ptr),hl

                        xor   a
      _loop             push  af
                        add   a,"0"
                        ld    (pagedbank.txt+4),a
                        sub   "0"
                        call  page_bank

                        ld    de,pagedbank.txt
                        call  print_text

                        ld    hl,14361          ; first contended cycle on early machine
                        call  do_test_set

                        ld    hl,14362          ; first contended cycle on late machine
                        call  do_test_set

                        pop   af
                        xor   7
                        jr    nz,_loop

                        call  page_bank

                        call  display_machine_timings
                        ret
                        endp

page_bank               proc
                        push  bc
                        ld    bc,#7ffd
                        or    16
                        out   (c),a
                        pop   bc
                        ret
                        endp

test_float_timing       proc

      _float_byte       equ   #aa

                        call  print_text_pc
                        db    22,0,0,16,1,"Float: ",16,2,255

                        ld    a,_float_byte
                        ld    (22528),a

                        ld    hl,14365-10-7           ; cycle to hit target address at
                        call  _do_float_test
                        ld    de,early.txt
                        cp    _float_byte
                        jr    z,_print_result

                        ld    hl,14366-10-7           ; cycle to hit target address at
                        call  _do_float_test
                        ld    de,late.txt
                        cp    _float_byte
                        jr    z,_print_result

                        ld    de,unknown.txt

      _print_result     call  print_text

                        call  print_text_pc
                        db    16,0,13,255

      _exit             ld    a,56
                        ld    (22528),a
                        ret

      _do_float_test    ld    ix,_read_float_bus      ; ix = target address
                        ld    de,default_exit_int     ; post-test exit interrupt address
                        call  exec_cycle_128k         ; run the test
                        ret

      _read_float_bus   ld    a,#ff
                        in    a,(#ff)
                        jp    default_exit_int
                        endp

; cycle count for this set of HALT addresses
do_test_set             proc
                        ld    (target_cycle),hl       ; store cycle count for this set of HALT addresses

                        ld    hl,16384
                        call  do_test

                        ld    hl,32767
                        call  do_test

                        ld    hl,32768
                        call  do_test

                        ld    hl,49151
                        call  do_test

                        ld    hl,49152
                        call  do_test

                        ld    hl,65535
                        call  do_test

                        ld    a,13
                        rst   16
                        ld    a,13
                        rst   16
                        ret
                        endp

; hl = target address
do_test                 proc

                        push  hl
                        pop   ix                      ; ix = target address
                        ld    (hl),#76                ; place a HALT opcode at target address

                        ld    de,num_txt
                        call  word_to_string
                        call  print_dec_16
                        ld    a,"/"
                        rst   16
                        ld    hl,(target_cycle)
                        ld    de,num_txt
                        call  word_to_string
                        call  print_dec_16
                        ld    a,":"
                        rst   16

                        ld    hl,(target_cycle)       ; cycle to hit target address at
                        ld    de,_read_R_exit_int     ; post-test exit interrupt address
                        call  exec_cycle_128k         ; run the test

                        ld    hl,(results_obtained_ptr)
                        ld    (hl),a
                        inc   hl
                        ld    (results_obtained_ptr),hl

                        ld    hl,0
                        ld    (16384),hl

                        call  print_hex_8

                        ld    a,6
                        rst   16
                        ret

      _read_R_exit_int  ld    a,r
                        jp    default_exit_int

                        endp


display_machine_timings proc

                        call  print_text_pc
                        db    22,0,16,16,1,"HALT: ",16,2,255

                        ld    de,results_early
                        call  comp_24_result_bytes
                        jr    z,_machine_early

                        ld    de,results_late
                        call  comp_24_result_bytes
                        jr    z,_machine_late

                        call  print_text_pc
                        db    "Unknown",16,0,13,255
                        ret

      _machine_early    call  print_text_pc
                        db    "Early",16,0,13,255
                        ret

      _machine_late     call  print_text_pc
                        db    "Late",16,0,13,255
                        ret
                        endp

comp_24_result_bytes    proc
                        ld    hl,results_obtained
                        ld    b,24
      _comp_loop        ld    a,(de)
                        cp    (hl)
                        ret   nz
                        inc   de
                        inc   hl
                        djnz  _comp_loop
                        ret
                        endp

print_dec_16            proc
                        call  print_text_pc
num_txt                 ds    5
                        db    255
                        ret
                        endp

                        include     execcycle.asm
                        include     delay.asm
                        include     print.asm

target_cycle            dw    0

early.txt               db    "Early",255
late.txt                db    "Late",255
unknown.txt             db    "Unknown",255

pagedbank.txt           db    "RAM x @ #C000",13,255

results_early           db    #3d, #3c, #3d, #3d, #3d, #3d
                        db    #3d, #3c, #3d, #3d, #3d, #3d
                        db    #3d, #3c, #3d, #3e, #3d, #3c
                        db    #3d, #3c, #3d, #3e, #3d, #3c

results_late            db    #3d, #3d, #3d, #3d, #3d, #3d
                        db    #3c, #3b, #3d, #3d, #3d, #3d
                        db    #3d, #3d, #3d, #3d, #3d, #3d
                        db    #3c, #3b, #3d, #3d, #3c, #3b

results_obtained_ptr    dw    0
results_obtained        ds    30

                        end   start


